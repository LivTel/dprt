#!/bin/csh
# $Id$
# By $Author: cjm $
# Make a configuration tarball file.
# This contains:
# 	The configuration files to run an instance of the DpRt
# Usage:
#	dprt_create_config_tarball <machine>
if ( ${#argv} != 1 ) then
	echo "Please specify a deployment machine."
	echo "$0 <machine>"
	exit 1
endif
set machine_name = "$1"
if ( "${machine_name}" == "-help") then
	echo "$0 <machine>"
	exit 0
endif
# dev root directory
set dev_dir 		= "~dev"
# dev binary root directory
set dev_bin_dir 	= "${dev_dir}/bin"
# directory script was run in
set current_dir		= `pwd`
# temporary directory containing root of deployment tree (which becomes root of tar).
set deployment_dir 	= "/tmp/dprt_deployment"
# temporary directory for deployment temporary files
set deployment_tmp_dir 	= "/tmp/dprt_deployment/tmp"
# temporary directory containing root icc directory.
set deployment_icc_dir 	= "/tmp/dprt_deployment/icc"
# temporary directory containing root bin directory.
set deployment_bin_dir 	= "/tmp/dprt_deployment/icc/bin"
# directory location of tar
set deployment_tar_dir 	= "${dev_dir}/public_html/dprt/deployment"
# name of tar file
set deployment_tar 	= "dprt_config_deployment_${machine_name}.tar"
#
# Clear old deployment.
#
if (-d ${deployment_dir}) then
	echo "Deployment directory ${deployment_dir} already exists:clearing..."
	rm -rf ${deployment_dir}
endif
#
# Create directory structure
#
echo "Creating directory structure..."
set dir_count = 0
set dir_list = "${deployment_dir} ${deployment_icc_dir} ${deployment_bin_dir} ${deployment_bin_dir}/dprt/java"
foreach dir ( ${dir_list} )
	mkdir -p ${dir}
	if ($status != 0) then
	   echo "Failed to make directory ${dir}"
	   exit 2
	endif
	@ dir_count ++
end
echo "${dir_count} directories created."
#
# Configs
#
# in bin sub-directories
echo "Copying machine configuration files..."
set file_list = "dprt/java/${machine_name}.dprt.properties"
set file_count = 0
cd ${dev_dir}
foreach file ( ${file_list} )
    set new_file = `echo ${file} | sed "s#${machine_name}.##"`
    cp ${dev_bin_dir}/${file} ${deployment_bin_dir}/${new_file}
    set cp_status = $status
    if( ${cp_status} != 0 ) then
	echo "Copy failed: ${dev_bin_dir}/${file} to ${deployment_bin_dir}/${new_file}"
	exit 5
    endif
    @ file_count ++
end
echo "${file_count} configs copied to bin dir."
#
# Create deployment tar
#
echo "--------------------------"
echo "Creating deployment tar..."
cd ${deployment_dir}
tar cvf ${deployment_tar_dir}/${deployment_tar} .
echo "GZipping deployment tar..."
cd ${deployment_tar_dir}
rm -f ${deployment_tar}.gz
gzip ${deployment_tar}
echo "Deployment tar : ${deployment_tar_dir}/${deployment_tar}.gz"
rm -rf ${deployment_dir}
echo "Download from http://ltdevsrv.livjm.ac.uk/~dev/loci/deployment/index.html."
echo "Download ${deployment_tar}.gz "
echo "Deployment ready."
